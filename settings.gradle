pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

Properties ghProperties = new Properties()
File ghPropertiesFile = file("github.properties")
if (ghPropertiesFile.exists()) {
    ghProperties.load(ghPropertiesFile.newDataInputStream())
}
else {
    ghProperties.setProperty('gpr.user', System.getenv("GH_USERNAME"))
    ghProperties.setProperty('gpr.key', System.getenv("GH_TOKEN"))
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/attentive-mobile/attentive-android-sdk")
            credentials {
                username = ghProperties.getProperty('gpr.user')
                password = ghProperties.getProperty('gpr.key')
            }
        }
    }
}
rootProject.name = "Attentive Android SDK"
include ':example'
include ':attentive-android-sdk'
include ':example-kotlin'
