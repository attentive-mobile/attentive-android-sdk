// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.compose) apply false
    alias(libs.plugins.google.services) apply false
    alias(libs.plugins.nexus.publish)
    alias(libs.plugins.ktlint)
    id 'checkstyle'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Apply ktlint to all subprojects with Kotlin
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        outputToConsole = true
        ignoreFailures = true  // Set to false once existing violations are fixed
        filter {
            exclude("**/AttentiveApi.kt")  // Contains fragile request strings
        }
    }
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
}

def checkstyleConfig = {
    group 'verification'
    configFile file('config/checkstyle/checkstyle.xml')
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    ignoreFailures = false
}

task checkExample(type: Checkstyle) {
    configure checkstyleConfig
    description 'Check code standard for Example app'
    source 'example'
}

task checkSdk(type: Checkstyle) {
    configure checkstyleConfig
    description 'Check code standard for SDK'
    source 'attentive-android-sdk'
}

task checkstyle() {
    group 'verification'
    description 'Check code standard'
    finalizedBy(checkExample)
    finalizedBy(checkSdk)
}

apply from: "${rootDir}/scripts/publish-root.gradle"
