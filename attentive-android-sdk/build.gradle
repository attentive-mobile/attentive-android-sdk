plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.compose)
}

def versionName = properties["VERSION_NAME"]

android {
    namespace = 'com.attentive.androidsdk'

    android.buildFeatures.buildConfig true

    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    lint {
        baseline = file("lint-baseline.xml")
        lintConfig = rootProject.file("lint.xml")
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "2.0.0"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    testFixtures {
        enable = true
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
            }
        }
        test {
            java {
                srcDirs 'src/test/java'
            }
        }
        androidTest {
            java {
                srcDirs 'src/androidTest/java'
            }
        }
    }
}

dependencies {
    // AndroidX
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.webkit)
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.activity)
    implementation(libs.androidx.lifecycle.process)
    implementation(libs.androidx.test.runner)

    // Google Material
    implementation(libs.google.material)

    // Logging
    implementation(libs.timber)

    // Networking
    api(libs.okhttp)
    implementation(libs.okhttp.logging)
    implementation(libs.retrofit)
    implementation(libs.retrofit.gson)
    implementation(libs.gson)

    // Serialization
    implementation(libs.kotlinx.serialization.json)

    // Coroutines
    implementation(libs.bundles.coroutines)

    // Compose
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.bundles.compose.sdk)

    // Image loading
    implementation(libs.bundles.coil)

    // Firebase
    implementation(platform(libs.firebase.bom))
    implementation(libs.firebase.messaging)

    // Testing
    testImplementation(libs.junit)
    testImplementation(libs.mockito.inline)
    testImplementation(libs.mockito.kotlin)
    testImplementation(libs.kotlinx.coroutines.test)

    androidTestImplementation(libs.androidx.test.junit)
    androidTestImplementation(libs.androidx.test.espresso)
    androidTestImplementation(libs.mockito.android)
}

ext {
    PUBLISH_GROUP_ID = 'com.attentive'
    PUBLISH_VERSION = "$versionName"
    PUBLISH_ARTIFACT_ID = 'attentive-android-sdk'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
