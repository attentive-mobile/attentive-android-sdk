plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = 'com.attentive.androidsdk'

    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }

    testFixtures {
        enable = true
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
            }
        }
        test {
            java {
                srcDirs 'src/test/java'
            }
        }
        androidTest {
            java {
                srcDirs 'src/androidTest/java'
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.webkit:webkit:1.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'

    // 'api' instead of 'implementation' because we allow the host app to pass in an okhttp object,
    // so we want to expose the okhttp object
    api 'com.squareup.okhttp3:okhttp:4.10.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.9.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

Properties ghProperties = new Properties()
ghProperties.load(project.rootProject.file("github.properties").newDataInputStream())


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.attentive'
                artifactId 'attentive-android-sdk'
                version '0.2.0'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/attentive-mobile/attentive-android-sdk")

                credentials {
                    username = ghProperties.getProperty('gpr.user', System.getenv("GH_USERNAME"))
                    password = ghProperties.getProperty('gpr.key', System.getenv("GH_TOKEN"))
                }
            }
        }
    }
}
