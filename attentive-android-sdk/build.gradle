plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.android'
}

def versionName = properties["VERSION_NAME"]

android {
    namespace = 'com.attentive.androidsdk'

    android.buildFeatures.buildConfig true

    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }

    kotlin {
        jvmToolchain(21)
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    testFixtures {
        enable = true
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
            }
        }
        test {
            java {
                srcDirs 'src/test/java'
            }
        }
        androidTest {
            java {
                srcDirs 'src/androidTest/java'
            }
        }
    }
}

dependencies {
    // build errors in unity for appcompat 1.1.0+
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.webkit:webkit:1.5.0'
    // 2.14.* does not support Android API Level < 26 https://github.com/FasterXML/jackson-databind/issues/3412
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    // Timber for logging - Upgrading to 5.+ versions causes compilation errors
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // 'api' instead of 'implementation' because we allow the host app to pass in an okhttp object,
    // so we want to expose the okhttp object
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'androidx.activity:activity:1.8.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"


    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'org.mockito:mockito-android:5.5.0'
}

ext {
    PUBLISH_GROUP_ID = 'com.attentive'
    PUBLISH_VERSION = "$versionName"
    PUBLISH_ARTIFACT_ID = 'attentive-android-sdk'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
